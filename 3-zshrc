#!/usr/bin/zsh
# Configuration for all interactive shells.
# Place to put generic ZSH options, handle plugins, named directories, etc.

# ===== Built-in options =====
# => Changing directories
setopt auto_cd              # 'cd' into directory given only its name
setopt no_auto_pushd        # do not interact with directory stack on 'cd'
setopt pushd_ignore_dups    # duplicate-free dirstack
# => Completion
setopt always_to_end        # always place curson at the end of inserted completion
setopt hash_list_all        # ensure new commands are hashed before completion
setopt list_rows_first      # list completions in rows (not in columns)
# => Globbing
setopt csh_null_glob        # report error only if no pattern matches
setopt extended_glob        # enable '^' and friends in globs
setopt glob_star_short      # **.c == **/*.c
setopt magic_equal_subst    # enable filename expansion in '--foo=file' params
setopt mark_dirs            # end directory names with '/'
setopt numeric_glob_sort    # sort by numbers, if possible
# => History
setopt extended_history     # save extra info in history file
setopt hist_expire_dups_first  # first expire duplicates when trimming
setopt hist_fcntl_lock      # use OS-provided file locking
setopt hist_find_no_dups    # ignore duplicates when searching
setopt hist_ignore_all_dups  # replace all duplicates
setopt hist_ignore_space    # do not remember commands beginning with space
setopt hist_reduce_blanks   # trim superfluous whitespace
setopt hist_save_no_dups    # omit older duplicates when writing the file
setopt hist_verify          # let user validate results of history expansion
setopt no_hist_beep         # do not beep on missing history entry
setopt share_history        # share history file between shells
# => I/O
setopt correct              # attempt to correct typos in commands
setopt no_flow_control      # disable ^S/^Q
# => Prompts
setopt transient_rprompt    # remove RPROMPT from display on <CR>
# =>Â ZLE
setopt no_beep              # do not beep. ever.


# ===== Named directories =====
# => ZSH configuration path
# ${(%):-%N} uses prompt expansion for locating currently sourced file (== this file)
# ${X:A:h} resolves symlinks and returns the dirname of X
hash -d zsh="${${(%):-%N}:A:h}"
# => ViM configuration path
[ -d "$HOME/.vim" ] && hash -d vim="${${:-$HOME/.vim}:A}"
# => Generic configuration path
[ -d "$HOME/system/dotfiles" ] && hash -d config="$HOME/system/dotfiles"
# => Source extra and host-specific directories
function {
    local -a extras=(
        ~zsh/"named-dirs.zsh"
        ~zsh/"host/$(hostname -s)/named-dirs.zsh"
    )
    for e in $extras; do [[ -r "$e" ]] && source "$e"; done
}


# ===== Aliases =====
# => Basic LS aliases
alias ls='ls --color=auto' l='ls -la' ll='ls -lh' lll='ls -lh --color=always|less -R'
# => Source extra and host-specific aliases
function {
    local -a extras=(
        ~zsh/"aliases.zsh"
        ~zsh/"host/$(hostname -s)/aliases.zsh"
    )
    for e in $extras; do [[ -r "$e" ]] && source "$e"; done
}

#
# ===== ZLE =====
# => keybinds
bindkey -v                  # Use vi-like key binds
bindkey "OA" up-line-or-search    # â†‘ search in history
bindkey "[A" up-line-or-search    # â†‘ search in history
bindkey "OB" down-line-or-search  # â†“ search in history
bindkey "[B" down-line-or-search  # â†“ search in history
# => extra functionality
autoload -Uz colors promptinit compinit zsh-mime-setup
colors                      # Initialize color arrays
promptinit                  # Initialize theme selection
compinit -d "$XDG_DATA_HOME/zsh/compdump" -i  # initialize completion, skipping insecure files
zsh-mime-setup              # Autocreate mime-based suffix aliases
# => Completion style
zstyle ':completion:*:descriptions' format "%{${fg[red]}%}[%d]%{$reset_color%}"
zstyle ':completion:*' group-name ''    # Use completion tag as a group name
zstyle ':completion:*' menu select      # Always use menu for selection
zstyle ':completion:*' rehash yes       # Rehash before completing external command
zstyle ':completion:*' verbose yes      # Display option description
# Try normal completion, then case-insensitive, then middle of word
zstyle ':completion:*' matcher-list '' 'm:{[:lower:][:upper:]}={[:upper:][:lower:]}' '+l:|=* r:|=*'
zstyle ':completion:*:default' list-colors ${(s.:.)LS_COLORS:-''}  # Color the completion menu
# => Prompt style
prompt khardix
# => Autoload all local functions
autoload -Uz ~zsh/autoload/*(*:t)


# ===== Plugin management =====
[ -r ~zsh/plugs.zsh ] && source ~zsh/plugs.zsh
