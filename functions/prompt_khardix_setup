#!/usr/bin/zsh
# Khardix's new prompt theme for zsh

autoload -Uz vcs_info

function prompt_khardix_help {
    cat <<'EOF'
This prompt is configurable via zsh styles:

    Colors (:prompt:khardix:color, use zsh/terminfo values):
        user-name: regular user name [default: 'red'].
        root-name: root user name [default: 208 (orange)].
        local-host: local host name [default: 'red'].
        remote-host: remote host name [default: 'yellow'].

        path: path components (working directory, etc.) [default: 'yellow'].
EOF
}

# Main theme setup function
function prompt_khardix_setup
{
    # Draw a block around another function result
    function block {
        local func=$1; shift
        local output="$($func)"

        [ "$output" ] && echo -n " %2{%B::%b%} $output"
    }

    # Current user and host name
    function identity {

        # Load colors from zstyle definitions
        local user root localhost remote
        zstyle -s :prompt:khardix:color user-name user; user=${user:-'red'}
        zstyle -s :prompt:khardix:color root-name root; root=${root:-'208'}
        zstyle -s :prompt:khardix:color local-host localhost; localhost=${localhost:-'red'}
        zstyle -s :prompt:khardix:color remote-host remote; remote=${remote:-'yellow'}

        local name_color="%(!.$root.$user)"
        local host_color="$(
            [[ -z "$SSH_CLIENT" ]] && echo "$localhost" || echo "$remote"
        )"

        # bold & colored name@host
        echo -n "%{%B%F{$name_color}%}%n%{%f%b%}@%{%B%F{$host_color}%}%m%{%f%b%}"
    }

    function working_directory {
        local path_color
        zstyle -s :prompt:khardix:color path path_color; path_color=${path_color:-'yellow'}

        echo -n "%{%F{$path_color}%}%3~%{%f%}"
    }

    function vcs_status {
        echo -n "${vcs_info_msg_0_}"
    }

    # Assemble prompt before each invocation
    function assemble_prompt {
        PS1="$(identity)$(block working_directory)$(block vcs_status) %(!.#.$) "
    }

    # Setup VCS display style
    zstyle ':vcs_info:git:*' get-revision 'true'
    zstyle ':vcs_info:git:*' formats 'Â±(%F{magenta}%b%f@%F{yellow}%.7i%f:%m)'

    # Register necessary hooks
    add-zsh-hook precmd vcs_info
    add-zsh-hook precmd assemble_prompt
}

prompt_khardix_setup "$@"
